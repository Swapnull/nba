message UpdateTeamProps {
    overrideDateProtection Boolean?
}
message UpdatePlayerTeamsProps {
    overrideDateProtection Boolean?
    teamId Text
}

message GetPlayersForTeamProps {
    teamId Text
}

message GetPlayersForTeamResponse {
    players Player[]
}

model Player {
    fields {
        externalId Text @unique
        firstName Text
        lastName Text
        position Text
        height Number
        weight Number
        jersey Text?
        birthDate Date
        highSchool Text?
        college Text?
        status Text
    }

    operations { 
        get getPlayer(id)
    }


    functions {
        read getPlayersOnTeam(GetPlayersForTeamProps) returns (GetPlayersForTeamResponse)
    }

    @permission(
        expression: true,
        actions: [create, update, get] 
    )

}

model PlayerTeam {
    fields {
        player Player
        team Team
        joined Date
        left Date?
    }

    functions {
        write updatePlayersOnTeam(UpdatePlayerTeamsProps) returns (Any)
    }

    @permission(
        expression: true,
        actions: [create, update, get] 
    )
}

model PlayerDraft {
    fields {
        player Player
        team Team
        year Number?
        round Number?
        pick Number?
    }

    @permission(
        expression: true,
        actions: [create, update, get] 
    )
}

model Team {
    fields { 
        externalId Text @unique
        name Text
        conference Text
        division Text
        alias Text?
    }

    operations {
        get getTeam(id) 
        list getTeams()
    }

    functions {
        write updateTeams(UpdateTeamProps) returns (Any)
    }

    @permission(
        expression: true,
        actions: [create, update, get, list] 
    )
}

model Game {
    fields {
        homeTeam Team
        awayTeam Team
        date Date
    }
}

model LastUpdate {
    fields {
        model Text 
        info Text 
        lastUpdate Timestamp
    }

    operations {
        update setLastUpdate(id) with (lastUpdate, model, info)
    }

    @permission(
        expression: true,
        actions: [create, update, get] 
    )
}

api Web {
    models {
        Player
        Team
        PlayerTeam
        PlayerDraft
        LastUpdate
    }
}
